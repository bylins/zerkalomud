#68300
Ворожея приветствует~
0 r 100
~
if (%quest683.name% == %nil%)
  думать %actor.iname%
  wait 1s
  г Здравствуй странник.
  г Приветствую тебя от имени жителей нашей деревни.
  г Чувствуй себя как дома.
  wait 1s
  г Но знай... Опасно стало здесь в последнее время.
  г Странные вещи стали происходить у лесного озера, что находится на западе.
  г Даже разбойники куда-то исчезли.
  думать
  wait 1s
  г Повылезли из своих подземных нор гады ползучие, да приноровились 
  г На рыбаков да на охотников нападать.
  взд
  wait 1s
  г Вижу тебе не впервой с такой ситуацией сталкиваться...
  г Может ты поможешь нам, а уж за наградой мы не постоим!
  вопрос %actor.iname%
elseif (%quest683.name% == %actor.iname%)
  wait 1s
  г Хм, ну как продвигаются твои поиски?
  вопрос %actor.iname%
else               
  взд
  г Ух! Еще гости пожаловали.
  г Да тут скоро вас толпы набегут!
  г Все поля повытопчите, все закрома опустошите, все вино выпьете!
  wait 1s
  г Не, хватит с меня всяких героев!
  г Нет у меня %actor.iname% для тебя ничего, иди куда ш%actor.y% и не мешайся.
end
~
#68301
Ворожея дает задание~
0 d 0
хорошо продолжай да помогу~
if (%quest683.name% == %nil%)
  хмур
  wait 1s 
  set quest683 %actor%
  global quest683
  г Дабы гадов ползучих истребить да в норы обратно загнать, нужны мне две вещи.
  г А именно Сверкающий шар и Белый цветок.
  г Только используя их силу мы сможем противостоять этой беде.   
  г И цветок и шар ты найдешь у озера.
  г Шаром когда-то владел шаман разбойников, но теперь он мертв.
  г Шаром завладел кто-то другой из их шайки, но вот кто, не знаю.
  г Белый цветок загадочен и таинственен. Ни кто не знает где именно он растет.
  г Знаю только, что он точно есть на озере.
  wait 1s
  г Запомни, в первую очередь мне нужен Сверкающий шар!
  г А цветок уже за ним!
  г Да, вот еще что.
  г Дабы все эти гады не зашли в деревню, мы выстроили ворота.
  г Вот, держи от них ключ.
  mload obj 68335
  дать ключ .%actor.iname%
  wait 1s
  г Это все, что тебе нужно знать.
  г Иди же, не стой здесь.
elseif (%quest683.name% == %actor.iname%)
  wait 1s
  думать %actor.iname%
  г Ты сюда языком лепетать приш%actor.y% или дело делать?!
  г Где то, что я просила тебя принести?
else                                  
  wait 1s
  плю %actor.iname%
  г Не лезь ко мне.
  г У меня, от вас героев, голова уже болеть начала!
end
~
#68302
Ворожея принимает задание~
0 j 100
~
if (%quest683.name% == %actor.iname%)
  if (%object.iname% == белый цветок) && (%give_ball_683% == 1)
    set give_flov_683 1
    global set give_flov_683
    wait 1s
    mpurge %object.name% 
    г Молодец. Это именно тот цветок, что мне нужен.
  elseif (%object.iname% == белый цветок) && (%give_ball_683% != 1)
    wait 1s
    г Я же сказала! Сначала дай мне шар!
    дать цветок %actor.iname%
  end
  if (%object.iname% == сверкающий шар) 
    set give_ball_683 1
    global give_ball_683
    wait 1s
    mpurge %object.name%
    mecho Ворожея взяла шар и внимательно посмотрела на него.
    г Да. Вот он! И он не потерял еще своей силы.
  end
  if (%give_flov_683% == 1) && (%give_ball_683% == 1)   
    wait 1s
    г Вот молодец!
    г Этим поступком ты очень помог%actor.q% нашей деревне.
    wait 1s
    mecho Ворожея взяла цветок и шар и начала внимательно их изучать.
    wait 3s
    г Да, это то что нужно! Я уверена. 
    думать %actor.iname%
    wait 1s
    г Вот твоя награда.
    set Get_item %random.5%
    set vnum 68327   
    eval vnum %vnum% + %Get_item%
    if (%vnum% > 68329)  || (%world.curobjs(%vnum%)% > 4)
      if %world.curobjs(3307)% < 2 && %random.10%  == 1
        mload obj 3307
        дать брас %actor.name%
      else
        mload obj 68330
        дать горсть %actor.iname%
      end
    else
      mload obj %vnum%
      дать перст %actor.iname% 
      дать кольц %actor.iname%
    end
    calcuid delete_trig 68307 mob
    detach 68300 %delete_trig.id% 
    detach 68301 %delete_trig.id%
    detach 68302 %delete_trig.id%
  end
end
~
#68303
Репоп~
2 f 100
~
set quest683 %nil%
global quest683
set give_flov_683 0
global give_flov_683
set give_ball_683 0
global give_ball_683
calcuid delete_trig 68307 mob
attach 68300 %delete_trig.id% 
attach 68301 %delete_trig.id%
attach 68302 %delete_trig.id%  
calcuid delete_trig 68305 mob
attach 68306 %delete_trig.id% 
wdoor 68362 south purge room 68318
wdoor 68318 north purge room 68362
~
#68304
Умирает злодей~
0 f 100
~
г Наконец-то я избавился от каждодневных страхов!
mecho Злодей судорожно закашлял и испустил дух.
mload obj 68327 
~
#68305
Двигать шкаф в избе кузнеца~
1 c 100
двигать~
oechoaround %actor% %actor.name% поднатужившись, сдвинул%actor.g% шкаф в сторону.
osend %actor% Немного поднатужившись, вы сдвинули шкаф в сторону.
odoor 68362 south room 68318
odoor 68318 north room 68362
oload obj 68334
opurge шкаф1 
~
#68306
Юный охотник приветствует~
0 r 100
~
wait 1s
г Здравствуй, путешественник.
г Зря ты зашел в эти темные места.
г Опасно здесь.
думать
wait 1s
г Но все же скажу тебе вот что...
г Здесь ты можешь встретить странных существ красного или синего цвета.
г Они очень опасны, но их кожа настолько прочна,
г Что из нее получаются великолепные вещи.
г Коли тебе удастся справиться с одним таким, попробуй снять с него кожу.
wait 1s
г Я же из нее смогу сделать какую-либо вещь.
calcuid delete_trig 68305 mob
detach 68306 %delete_trig.id% 
~
#68307
Юный охотник делает вещи~
0 j 100
~
wait 1
switch %object.vnum%
  case 68324
    wait 1
    mpurge %object%
    wait 1s 
    г Хорошо, я посмотрю, что смогу сделать... подожди немного.
    wait 3s
    set Getitem %random.30%
    set vnum 68299   
    eval vnum %vnum% + %Getitem%
    if (%vnum% > 68308)  || (%world.curobjs(%vnum%)% > 3)
      wait 1s
      г Эх. Ничего не получилось. Видно у меня еще мало опыта.
      г Приноси еще, может чего и надумаю.
    else
      wait 1s
      г Вот, гляди, что я для тебя смастерил!
      mload obj %vnum%
      дать все .%actor.iname%
      улыб %actor.iname%
    end
  break
  case 68325
    wait 1
    mpurge %object%
    wait 1s 
    г Хорошо, я посмотрю, что смогу сделать... подожди немного.
    wait 3s
    set Getitem %random.30%
    set vnum 68308   
    eval vnum %vnum% + %Getitem%
    if (%vnum% > 68316)  || (%world.curobjs(%vnum%)% > 3)
      wait 1s
      г Эх. Ничего не получилось. Видно у меня еще мало опыта, по работе с этой кожей.
      г Приноси еще, может чего и надумаю.
    else
      wait 1s
      г Вот, гляди, что я для тебя смастерил!
      mload obj %vnum%
      дать все .%actor.iname%
      улыб %actor.iname%
    end
  break
  default
    дум
    drop all
  done
~
#68308
Снимает красную кожу~
0 f 100
~
set rand %random.5%
if (%rand% == 1)
  msend %actor.iname% Вы брезгливо сняли кожу с трупа красного озерного чудища.
  mechoaround %actor.iname% %actor.iname% брезгливо снял%actor.g% кожу с трупа красного озерного чудища.
  mload obj 68324
end
~
#68309
Снимает синюю кожу~
0 f 100
~
set rand %random.5%
if (%rand% == 1)
  msend %actor.iname% Вы брезгливо сняли кожу с трупа синего озерного чудища.
  mechoaround %actor.iname% %actor.iname% брезгливо снял%actor.g% кожу с трупа синего озерного чудища.
  mload obj 68325
end
~
#68310
Цветок падает с пятиглавого чудища~
0 f 100
~
if (%random.100% <= 15) && (%world.curobjs(209)% < 50)
  mload obj 209
end
mecho Воздух содрогнулся и яркая вспышка озарила небо.
mecho Когда все рассеялось, на трупе убитого пятиглавого чудища вы увидели
mecho Нежный белый цветок.
mload obj 68326
~
#68311
встреча в кожемякой~
0 r 100
~
wait 1
шляп %actor.name%
wait 1
say А кузнец-то наш хитер! Иж чего в хате своей выдумал!
say И спрашивается - зачем оно ему надо?    
say Ну не дурак ли?
~
#68312
у кузнеца~
0 r 100
~
wait 1
say Вот ерш твою медь! Ну куда он мог запропаститься? Вчера только видел!
say И ведь ну никому же даром не сдался! Назло мне сперли!
wait 1
осм %actor.name%
say Не видел тут нигде не валялся бронзовый ухналь? Гвоздик такой маленький!
say Вещь в хозяйстве совсем бесполезная, да память о тятеньки моем.
say Он тоже кузнецом был...
wait 1
руг
~
#68313
дали ухналь кузнецу~
0 j 100
~
if %object.vnum% == 68336 then
  wait 2s
  mpurge %object%
  say О! нашелся ухналь-то!...Вот радость...
  wait 2s
  if %actor.level% > 20
    msend       %actor% _За доброе дело Вы получили 5000 очков опыта...
    %actor.exp(+5000)%  
  end 
  say Ну спасибо, земляк! Выручил!
  say А теперь ступай себе с Богом, не мешай работать! А то по темечку молотом стукну!
  ул
  mecho Кузнец осенил Вас крестом и вновь стал раздувать меха горна.
end
~
#68314
благодарность лекаря~
0 j 100
~
wait 1
if %object.vnum% != 68337
  брос все
  halt
end
wait 1
mpurge %object%
say Так, замечательно! Я наверняка знаю чья это печень!
say На прошлой неделе мы похоронили одного крестьянина - от пьянки он умер!
say Видишь какая печенка то у него вздувшаяся! Водка - яд!
say Однако хватит байки травить, я займусь приготовлениями к обряду...
say А ты заслуживаешь, безусловно, награды!
switch %random.20%
  case 1
    mload obj 526
    дать все %actor.name%
  break
  case 2
    mload obj 520
    дать все %actor.name%
  break
  default
    wait 10
    if (%world.curobjs(206)%) < 50 && (%random.100% < 15)
      mload obj 206
      дать все %actor.name%
    else
      mecho Лекарь достал что-то из кармана.
      %self.gold(3000)%
      дать 3000 кун %actor.name%
    end
  done
  detach 68314 %self.id%
~
#68315
вошли к лекарю~
0 r 100
~
wait 2    
осм %actor.name%
say Рад видеть тебя.
wait 3
say Хотя не скрою, что времена нынче не из радостных.
say То разбойники в здешних лесах христиан обижают
крест
wait 3
say Теперь вот твари эти повыползали из озера...
груст
say Но есть кое-что и пострашнее чудищ озерных.
wait 5
взд
say К северу от озера видели охотники недавно усопших наших односельчан
say И клянуться рассказчики, что трезвы они были, а дескать видели ходячих мертвецов!
взд
wait 5
say Я может в другой раз и не поверил бы байкам охотничьим, но тут душа моя не спокойна!
say Сон я дурной видел... сходил бы ты туда, посмотрел что там творится на самом деле?
wait 1
say Поможешь?
end
~
#68316
помочь лекарю~
0 d 100
помогу~
wait 5
улы
say Если все и вправду там, как рассказывают, то надобны успокоить мертвецов!
say Принеси мне любую часть тела ожившего трупа, я совершу над ней святой обряд...
say Это должно будет вернуть покойников в могилы!
attach 68314 %self.id%
detach 68315 %self.id%
detach 68316 %self.id%
end
~
#68317
убили труп~
0 f 100
~
if %world.curobjs(68337)% <5
  mload obj 68337
end
~
$~
